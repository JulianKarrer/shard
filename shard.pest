WHITESPACE   =  _{ " " | "\t" }
program      =   { SOI ~ (nl* ~ function ~nl*)+ ~ EOI }
  // utf-8 identifiers
  utfsym     =  _{ LETTER | SYMBOL }
  utfnum     =  _{ LETTER | SYMBOL }
  ident      =  @{ utfsym ~ (utfnum | utfsym)* }
  // assignments
  assign     =  { ident ~ ":=" ~ expr }
  // functions
  nl         = _{NEWLINE}
  function   =  { signature ~ nl ~ scope }
  numtype    =  { "1"|"2"|"3"|"4" }
  arg        =  { ident ~ ":" ~ numtype }
  signature  =  { "fn " ~ ident ~ "(" ~  (arg ~ ("," ~ arg)*)?  ~ ")" ~ "->" ~ numtype }
  scope      =  { nl*~ (assign ~ nl)* ~ expr ~ nl*}
  // expressions
  expr       =   { prefix? ~ primary ~ postfix* ~ (infix ~ prefix? ~ primary ~ postfix* )* }
    infix    =  _{ add | sub | mul | div  }
      add    =   { "+" } // Addition
      sub    =   { "-" } // Subtraction
      mul    =   { "*" } // Multiplication
      div    =   { "/" } // Division
    prefix   =  _{ neg }
      neg    =   { "-" } // Negation
    postfix  =  _{ "." ~ (x|y|z|w|sin|fract|length) }
      // built in unaries
      x       = { "x" }
      y       = { "y" }
      z       = { "z" }
      w       = { "w" }
      sin     = { "sin" }
      fract   = { "fract" }
      length  = { "length" }
    primary  =  _{ num | uniform | ident | "(" ~ expr ~ ")" }
    num = { float | int }
    float	= @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+}
    int    =  @{ 
      (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) 
    }
    uniform = { "uv" | "time" }
